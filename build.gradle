plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7' apply false
    id 'io.spring.dependency-management' version '1.1.5' apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:3.5.7"
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    // Skip tests
    tasks.named('test') {
        enabled = false
    }

    // Configure Java compilation with preview features
    tasks.named('compileJava') {
        options.compilerArgs += ['--enable-preview', '-Xlint:preview']
        options.encoding = 'UTF-8'
    }

    tasks.named('compileTestJava') {
        options.compilerArgs += ['--enable-preview', '-Xlint:preview']
        options.encoding = 'UTF-8'
    }

    // Configure bootRun and test to use preview features
    tasks.named('bootRun') {
        jvmArgs += ['--enable-preview']
    }

    tasks.named('test') {
        jvmArgs += ['--enable-preview']
    }
}